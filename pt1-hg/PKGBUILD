# Maintainer: Yoshihisa Tanaka <yt.hisa@gmail.com>
pkgname=pt1-hg
pkgver=129
pkgrel=1
pkgdesc='pt1 driver and enhanced recording tool'
arch=('i686' 'x86_64')
url='http://hg.honeyplanet.jp/pt1/'
license=('GPL')
depends=('arib25')
makedepends=('mercurial' 'autoconf' 'linux-headers')
provides=(pt1)
install=pt1.install
source=("driver-make.patch")
md5sums=('0bec905dcb637f80ae9da25ecd18fb93')
_hgroot="http://hg.honeyplanet.jp"
_hgrepo="pt1"

build() {
  cd "$srcdir"

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u
  else
    hg clone "$_hgroot/$_hgrepo"
  fi

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  patch -Np0 -i "$srcdir/driver-make.patch" || return 1
  cd driver
  make || return 1

  cd ../recpt1
  ./autogen.sh || return 1
  ./configure --prefix=/usr --enable-b25 || return 1
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"
  cd driver
  install -Dm 644 pt1_drv.ko "$pkgdir/lib/modules/`uname -r`/kernel/drivers/video/pt1_drv.ko"
  install -Dm 644 etc/99-pt1.rules "$pkgdir/etc/udev/rules.d/99-pt1.rules"

  cd ../recpt1
  mkdir -p "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
